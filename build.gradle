/**
 * Plug-in development
 */

plugins {
   id 'nu.studer.plugindev' version '1.0.3'
   id 'com.jfrog.bintray' version '1.3.1'
    /**
     * Versioning applied to itself
     */
    id 'net.nemerosa.versioning' version '2.5.0'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.9.7'
}

/**
 * Meta information
 */

group = 'coppernic'
version = versioning.info.display

/**
 * Dependencies
 */

ext {
    jgitVersion = '4.5.0.201609210915-r'
}

dependencies {
    localGroovy()
    compile 'org.ajoberstar:grgit:1.7.0'
    compile "org.eclipse.jgit:org.eclipse.jgit.ui:${jgitVersion}"
    compile "org.eclipse.jgit:org.eclipse.jgit:${jgitVersion}"
    compile 'org.tmatesoft.svnkit:svnkit:1.8.12'
    testCompile "junit:junit:4.12"
    testCompile "commons-lang:commons-lang:2.6"
    testCompile "commons-io:commons-io:2.5"
}

/**
 * Plug-in definition
 */

plugindev {
    pluginImplementationClass 'net.nemerosa.versioning.VersioningPlugin'
    pluginDescription 'Gradle plug-in that computes version information from the SCM'
    pluginLicenses 'MIT'
    pluginTags 'gradle', 'plugin', 'scm', 'git', 'svn', 'version'
    authorId 'dcoraboeuf'
    authorName 'Damien Coraboeuf'
    authorEmail 'damien.coraboeuf@gmail.com'
    projectUrl 'https://github.com/nemerosa/versioning'
    projectInceptionYear '2015'
    done()
}

pluginBundle {
    website = 'https://github.com/CoppernicSoftware/versioning'
    vcsUrl = 'https://github.com/CoppernicSoftware/versioning'
    description = 'Gradle plug-in that computes version information from the SCM'
    tags = ['gradle', 'plugin', 'scm', 'git', 'svn', 'version']

    plugins {
        gitExtractorPlugin {
            id = 'fr.coppernic.versioning'
            displayName = 'Versioning plugin'
        }
    }
}

/**
 * Wrapper definition
 */

task wrapper(type: Wrapper) {
   gradleVersion = '2.13'
}

test {
  environment "GIT_TEST_BRANCH", "feature/456-cute"
  environment "SVN_TEST_BRANCH", "feature-456-cute"
}
